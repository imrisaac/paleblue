# Copyright (C) 2022 The Qt Company Ltd.
# SPDX-License-Identifier: BSD-3-Clause

cmake_minimum_required(VERSION 3.16)
project(photosurface LANGUAGES CXX)

set(CMAKE_AUTOMOC ON)

if(NOT DEFINED INSTALL_EXAMPLESDIR)
    set(INSTALL_EXAMPLESDIR "examples")
endif()

set(INSTALL_EXAMPLEDIR "/home/root/photosurface")

find_package(Qt6 REQUIRED COMPONENTS Core Gui Qml Quick QuickControls2)

qt_standard_project_setup(REQUIRES 6.5)

if (WIN32)
#! [appicon_windows]
    set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/resources/photosurface.rc")
    qt_add_executable(photosurfaceexample main.cpp ${app_icon_resource_windows})
#! [appicon_windows]
elseif (APPLE)
#! [appicon_macOS]
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE photosurface.icns)

    # And the following tells CMake where to find and install the file itself.
    set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/photosurface.icns")
    set_source_files_properties(${app_icon_macos} PROPERTIES
           MACOSX_PACKAGE_LOCATION "Resources")

    qt_add_executable(photosurfaceexample MACOSX_BUNDLE main.cpp ${app_icon_macos})
#! [appicon_macOS]
else()
    qt_add_executable(photosurfaceexample main.cpp)
endif()

target_link_libraries(photosurfaceexample PRIVATE
    Qt::Core
    Qt::Gui
    Qt::Qml
    Qt::Quick
)

qt_add_qml_module(photosurfaceexample
    URI photosurface
    QML_FILES
      "photosurface.qml"
      "resources/MomentumAnimation.qml"
      "WifiButton.qml"
      "NetworkSettings/CustomSwitch.qml"
      "NetworkSettings/NetworkDelegate.qml"
      "NetworkSettings/NetworkListView.qml"
      "NetworkSettings/NetworkSettingsPage.qml"
      "NetworkSettings/PassphraseEnter.qml"
      "NetworkSettings/QtButton.qml"
      "NetworkSettings/ViewSettings.qml"
    RESOURCES
      "resources/folder.png"
      "resources/east_10848x10848.jpg"
      "resources/west_10848x10848.jpg"
      SOURCES imageitem.h imageitem.cpp
      SOURCES filedownloader.h filedownloader.cpp
      SOURCES paleblue.h paleblue.cpp
      SOURCES timemanager.h timemanager.cpp
      SOURCES settingsmanager.h settingsmanager.cpp
      SOURCES qtbuttonimageprovider.h qtbuttonimageprovider.cpp
      RESOURCES qml.qrc
)

install(TARGETS photosurfaceexample
    RUNTIME DESTINATION "${INSTALL_EXAMPLEDIR}"
    BUNDLE DESTINATION "${INSTALL_EXAMPLEDIR}"
    LIBRARY DESTINATION "${INSTALL_EXAMPLEDIR}"
)
